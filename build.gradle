// add gradle-cargo-plugin plugin dependency
plugins {
    id 'java'
    id 'war'
    id 'com.bmuschko.cargo' version '2.2.2'
}

// use Java 1.8
sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

// In this section you declare where to find the dependencies of your project
repositories {
    // Use 'jcenter' for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

// In this section you declare the dependencies for your production and test code
dependencies {
    // Java EE 7 API
    compile 'javax:javaee-api:7.0'

    // specify the cargo library version
    def cargoVersion = '1.4.19'
    cargo "org.codehaus.cargo:cargo-core-uberjar:$cargoVersion"
    cargo "org.codehaus.cargo:cargo-ant:$cargoVersion"

    // The production code uses the SLF4J logging API at compile time
    compile 'org.slf4j:slf4j-api:1.7.21'

    // Declare the dependency for your favourite test framework you want to use in your tests.
    // TestNG is also supported by the Gradle Test task. Just change the
    // testCompile dependency to testCompile 'org.testng:testng:6.8.1' and add
    // 'test.useTestNG()' to your build script.
    testCompile 'junit:junit:4.12'
}

// setup cargo to automatically download WildFly and fire it when deploying to local
cargo {
    containerId = 'wildfly10x'

    deployable {
        // deploy to the context of the project name defined in settings.gradle
        // context = project.name
        // or deploy to the ROOT
        context = '/'
    }

    local {
        outputFile = file('build/wildfly/wildfly.out')
        containerProperties { property 'cargo.jboss.configuration', 'standalone-full' }

        installer {
            installUrl = 'https://download.jboss.org/wildfly/10.0.0.Final/wildfly-10.0.0.Final.zip'
            downloadDir = file("download")
            extractDir = file("wildfly")
        }
    }
}
